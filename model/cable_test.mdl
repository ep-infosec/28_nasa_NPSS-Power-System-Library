/***
 -------------------------------------------------------------------------------
 |                                                                             |
 | NASA Glenn Research Center                                                  |
 | 21000 Brookpark Rd 		                                                     |
 | Cleveland, OH 44135 	                                                       |
 |                                                                             |
 | File Name:     cable_test.mdl                                               |
 | Author(s):	    George Thomas, Jeffrey Csank, David Sadey, Tom Lavelle       |
 | Date(s):       February 2019                                                |
 |                                                                             |
 | Description:   Run the cable element on and off-design.                     |
 |                                                                             |
 -------------------------------------------------------------------------------
***/

class Source extends Element{
	ElectricOutputPort EP_O;
	ElectricInputPort EP_I;
	real Vreal = 500.;
	real Vimag = 0.;
	real Pdes = 1000.;
	real PFdes = 0.95;
	
	Independent ind_Vreal { 
		varName = "Vreal"; 
		autoSetup = TRUE;
		a = 10000.;
	}
	
	Independent ind_Vimag { 
		varName = "Vimag"; 
		autoSetup = TRUE;
		a = 10000.;
	}
	
	Dependent dep_Power{
		eq_rhs="EP_O.S.rRMS";
		eq_lhs="Pdes";
		autoSetup=TRUE;
	}
	
	Dependent dep_PF{
		eq_rhs="cos(EP_O.S.phase)";
		eq_lhs="PFdes";
		autoSetup=TRUE;
	}
}

real defaultFrequency = 400.;

Source ACS{
	// Set up frequency
	EP_O.frequency = defaultFrequency;
	EP_I.frequency = defaultFrequency;
	void prePass(){
		//EP_O.setIVRMS( 0, 0., 15., 8.66025403784438); // This is the VLL representation of VLN = 10+j0
		EP_O.setIVRMS( 0, 0., 1000., 0.);
		EP_I.setIVRMS( 0, 0., Vreal, Vimag );
	}
}
Element AeroCable ACCable {
	length=30;
}
ACS.EP_O.setOption("ElectricPowerType","AC3");
ACS.EP_I.setOption("ElectricPowerType","AC3");
ACCable.EP_O.setOption("ElectricPowerType","AC3");
ACCable.EP_I.setOption("ElectricPowerType","AC3");


Source DCS{
	// No imaginary stuff for a DC source
	ind_Vimag.autoSetup = FALSE;
	dep_PF.autoSetup = FALSE;
	void postexecute(){
		EP_O.setIVRMS( 0, 0., 1000., 0.);
		EP_I.setIVRMS( 0, 0., Vreal, 0. );
	}
}
Element AeroCable DCCable {
	length=30;
}
DCS.EP_O.setOption("ElectricPowerType","DC");
DCS.EP_I.setOption("ElectricPowerType","DC");
DCCable.EP_O.setOption("ElectricPowerType","DC");
DCCable.EP_I.setOption("ElectricPowerType","DC");

linkPortI( "ACS.EP_O", "ACCable.EP_I" );
linkPortI( "ACCable.EP_O", "ACS.EP_I" );
linkPortI( "DCS.EP_O", "DCCable.EP_I" );
linkPortI( "DCCable.EP_O", "DCS.EP_I" );
